apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "me.jemaystermind.buildconfigtraining"

        minSdkVersion 21
        targetSdkVersion 28

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        training {
            storeFile rootProject.file('config-training.keystore')
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.training
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
        fake {
            // Copy configurations from 'debug' build type
            initWith debug
            applicationIdSuffix ".debugFake"
        }
    }

    flavorDimensions 'place'
    productFlavors {
        asia {
            dimension "place"
            applicationIdSuffix ".asia"
            versionNameSuffix "-asia"
        }

        swiss {
            dimension "place"
            applicationIdSuffix ".swiss"
            versionNameSuffix "-swiss"
        }

        monaco {
            dimension "place"
            applicationIdSuffix ".monaco"
            versionNameSuffix "-monaco"
        }
    }
    sourceSets {
        release {
            // Add additional source directory on top of src/main/java
            java.srcDirs += ['src/common/java']
        }

        debug {
            // Add additional source directory on top of src/main/java
            java.srcDirs += ['src/common/java']
        }
    }
}

dependencies {
    implementation project(':api-service')
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0-alpha3'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
